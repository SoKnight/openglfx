plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id "application"
}

project.ext.mavenModuleName = "lwjgl"
apply from: "${project.rootDir}/maven-publisher.gradle"

sourceSets{
    examples {
        resources { srcDirs "src/examples/resources" }
        java {
            srcDirs 'src/examples/kotlin', 'src/examples/java'
            compileClasspath += sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
            runtimeClasspath += sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
        }
    }
}

dependencies {
    compileOnly DEFAULT
    compileOnly project(':core')

    compileOnly platform("org.lwjgl:lwjgl-bom:${project["version.lwjgl"]}")
    compileOnly "org.lwjgl:lwjgl"
    compileOnly "org.lwjgl:lwjgl-opengl"

    compileOnly "org.lwjgl:lwjgl::$project.lwjglNatives"
    compileOnly "org.lwjgl:lwjgl-opengl::$project.lwjglNatives"
}

tasks.register('runLWJGLD3D', JavaExec) {
    group = "Execution"
    description = "Run the example class with LWJGL D3D"
    classpath = sourceSets.examples.runtimeClasspath
    mainClass = "LWJGL_D3DKt"
    jvmArgs project["opens"].split()
}

tasks.register('runLWJGLSW', JavaExec) {
    group = "Execution"
    description = "Run the example class with LWJGL SW"
    classpath = sourceSets.examples.runtimeClasspath
    mainClass = "LWJGL_SWKt"
    jvmArgs project["opens"].split()
}

tasks.register('runLWJGLES2', JavaExec) {
    group = "Execution"
    description = "Run the example class with LWJGL ES2"
    classpath = sourceSets.examples.runtimeClasspath
    mainClass = "LWJGL_ES2Kt"
    jvmArgs project["opens"].split()
}
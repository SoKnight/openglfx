// Natives variables for LWJGL and JFX
def os = org.gradle.internal.os.OperatingSystem.current()
def osArch = System.getProperty("os.arch")
def arm64 = osArch.startsWith("aarch64") || osArch.startsWith("armv8")
def arm32 = osArch.startsWith("arm") && !arm64
def x64 = osArch.contains("64") && !arm32 && !arm64
def x86 = !x64 && !arm32 && !arm64

def lwjglArch = arm64? "-arm64" :arm32? "-arm32" :x64? "" :x86? "-x86" : ""
def lwjglOS = os.linux? "linux" :os.macOsX? "macos" :os.windows? "windows" : os.name
project.ext.lwjglNatives = "natives-${lwjglOS + lwjglArch}"

def joglArch = arm64? "-armv6hv" :arm32? "-armv6" :x64? "-amd64" :x86? "-i586" : ""
def joglOS = os.linux? "linux" :os.macOsX? "macosx" :os.windows? "windows" : os.name
project.ext.joglShortOS = os.linux? "x11" :os.macOsX? "osx" :os.windows? "win" : os.name
project.ext.joglNatives = joglOS + (os.macOsX ? "-universal" : joglArch)

def jfxArch = arm64? "-aarch64" :arm32? "-arm32" :x64? "" :x86? "-x86" : ""
def jfxOS = os.linux? "linux" :os.macOsX? "mac" :os.windows? "win" : os.name
project.ext.jfxNatives = jfxOS + jfxArch

ext {
    JavaFX = [
            "org.openjfx:javafx-base:$javafxVersion:$project.jfxNatives",
            "org.openjfx:javafx-controls:$javafxVersion:$project.jfxNatives",
            "org.openjfx:javafx-graphics:$javafxVersion:$project.jfxNatives",
    ]
}